autoload promptinit
promptinit
setopt prompt_subst

# Show time for long running processes.
REPORTTIME=2

host_color=$(hash_color $(hostname))
#host_color=$(hash_color radium)

autoload -Uz vcs_info
if vcs_info 2> /dev/null; then
    zstyle ':vcs_info:*' enable git
    zstyle ':vcs_info:git*:*' get-revision true
    zstyle ':vcs_info:git*:*' check-for-changes true

    zstyle ':vcs_info:*' actionformats "%s%c%u %r/%S:%F{7}%b%F{85}#%0.6i%f:%a"
    zstyle ':vcs_info:*' formats       "%s%c%u %r/%S:%F{7}%b%F{85}#%0.6i%f"
fi

# Called right before a command runs
preexec_prompt () {
    resetpromptcount="0"
    # post-command prompt gets a long time and a short tty
    lasttime=""
    ODIR="$(pwd)"
}

# Called right before drawing a prompt.
precmd_prompt () {
    lastexit=$?
    pscwd=$(pwd | sed -e "s|$HOME|~|")
    pspath="%F{green}${pscwd}%f"
    tri=''

    if [[ $lastexit -eq 0 ]]; then
        ret_color=29
    else
        ret_color=160
    fi
    if [[ -n $AUTO_VENV_LAST_DIR ]]; then
        venv_color=255
        venv_name=$(basename $AUTO_VENV_LAST_DIR)
    else
        venv_color=232
    fi

    psvenv="%F{$venv_color}%K{${ret_color}}${tri}"
    psret="%F{${ret_color}}%K{${host_color}}${tri}%f%k"
    psglyph="%F{${host_color}}${tri}%f%k"

    vcs_info
    if [[ -n $vcs_info_msg_0_ ]] ; then
        # Turn foo/bar/. into foo/bar in paths.
        psvcs=$(echo $vcs_info_msg_0_ | sed -e 's_^\([^/]\+\)/\._\1_')
        # If branch name is master, hide it.
        psvcs=$(echo $psvcs | sed -e 's/:%F{[0-9]\+}master//')
        # Replace VCS name and status with stop light colored logo
        psvcs=$(echo $psvcs | sed -e 's/gitSU/%F{yellow}±%F{blue}/')
        psvcs=$(echo $psvcs | sed -e 's/gitS/%F{green}±%F{blue}/')
        psvcs=$(echo $psvcs | sed -e 's/gitU/%F{red}±%F{blue}/')
        psvcs=$(echo $psvcs | sed -e 's/git/±/')
        # turn it blue
        psvcs="%F{blue}${psvcs}%f"
        pspath=""
    else
        psvcs=""
    fi

    if [[ -n $venv_name ]]; then
        PS1=$(echo $PS1 | sed -e "s/($venv_name)//")
        pspath=$(echo $pspath | sed -e "s|$venv_name|[$venv_name]|")
        psvcs=$(echo $psvcs | sed -e "s|$venv_name|[$venv_name]|")
    fi
}

add-zsh-hook preexec preexec_prompt
add-zsh-hook precmd precmd_prompt

# ►
PROMPT='${psvenv}${psret}${psglyph} '
RPROMPT='${pspath}${psvcs}'
